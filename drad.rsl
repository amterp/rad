#!/usr/bin/env rad
---
Developing rad.
---
args:
    version v int = 0 # 1 to bump patch, 2 for minor and 3 for major.
    no_push "no-push" P bool # Enable to not push.

$!`make all`

if version:
    // resolve version /////////////
    _, tags = $!`git tag`
    tags = split(tags[:-1], "\n")
    tags = [replace(t, "v", "") for t in tags]
    tags = [split(t, "\.") for t in tags]
    majors = sort([parse_int(t[0]) for t in tags])
    minors = sort([parse_int(t[1]) for t in tags])
    patches = sort([parse_int(t[2]) for t in tags])
    major = majors[-1]
    minor = minors[-1]
    patch = patches[-1]
    if version == 1:
        version = "{major}.{minor}.{patch + 1}"
    else if version == 2:
        version = "{major}.{minor + 1}.0"
    else if version == 3:
        version = "{major + 1}.0.0"
    ///////////////////////////////
    
    $!`go test ./core/testing -count 30`

    _, mod = quiet $!`cat go.mod`
    defer:
        quiet $!`echo -n '{mod}' > go.mod`
    $!`go mod edit --dropreplace=github.com/amterp/rts`
    $!`go mod edit --dropreplace=github.com/amterp/tree-sitter-rsl`

    clean_before = unsafe $`git status --porcelain | grep -q .`

    $!`sed -i '' "s/Version = \".*\"/Version = \"{version}\"/" ./core/version.go`

    $!`git add .`
    $!`git diff HEAD --compact-summary`

    if confirm("Commit, tag & push? ({version}) [y/n] > "):
        _, branch  = $!`echo -n $(git branch --show-current)`
        
        if clean_before:
            // implied only our sed version bump made a diff
            $!`git commit -m "Bump version to {version}"`
        else:
            $!`git commit`
        $!`git tag -a "{version}" -m "Bump version to {version}"`
        push_cmd = `git push origin {branch} --tags`
        if no_push:
            print("Not pushing, remember to run", yellow(push_cmd), "later")
        else:
            $!push_cmd

print(green("âœ… Done!"))
