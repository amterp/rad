#!/usr/bin/env rad
---
Assists in running/developing anything rad-related.
---
args:
    rad bool # Enable to build rad.
    rls bool # Enable to rebuild RLS.
    vsc bool # Enable to rebuild VSC extension.
    docs d bool # Enable to deploy docs.
    version v int = 0 # 1 to bump patch, 2 for minor and 3 for major.
    commit c bool # Enable to create a commit. Stashes local go.mod replaces first.
    test_count "test-count" t int = 30 # The number of times to run tests, to detect flakiness.
    no_push "no-push" P bool # Disables pushing.

    version regex "^[0-9]+\.[0-9]+\.[0-9]+$"

if rad:
    $!`make all`

if rls:
    $!`cd rsl-language-server && make all`

if vsc:
    $!`cd vsc-extension && npm install && npm run compile`

if docs:
    $!`mkdocs gh-deploy -f ./docs-web/mkdocs.yml`

if commit:
    _, mod = quiet $!`cat ./rsl-language-server/go.mod`
    defer:
        $!`echo -n '{mod}' > ./rsl-language-server/go.mod`
    $!`cd rsl-language-server && go mod edit --dropreplace=github.com/amterp/rts`
    $!`git add .`
    clean = unsafe $`git status --porcelain | grep -q .`
    if not clean:
        $!`git diff HEAD --compact-summary`
        if confirm("Commit? [y/n] > "):
            $!`git commit`

if version:

    // resolve version /////////////
    _, tags = $!`git tag`
    tags = split(tags[:-1], "\n")
    tags = [replace(t, "v", "") for t in tags]
    tags = [split(t, "\.") for t in tags]

    majors = sort([parse_int(t[0]) for t in tags])
    minors = sort([parse_int(t[1]) for t in tags])
    patches = sort([parse_int(t[2]) for t in tags])

    major = majors[-1]
    minor = minors[-1]
    patch = patches[-1]

    if version == 1:
        version = "{major}.{minor}.{patch + 1}"
    else if version == 2:
        version = "{major}.{minor + 1}.0"
    else if version == 3:
        version = "{major + 1}.0.0"
    ///////////////////////////////

    if not rad:
        $!`make all`
        $!`go test ./core/testing -count {test_count}`

    _, mod = quiet $!`cat ./rsl-language-server/go.mod`
    defer:
        $!`echo -n '{mod}' > ./rsl-language-server/go.mod`
    $!`cd rsl-language-server && go mod edit --dropreplace=github.com/amterp/rts`

    clean_before = unsafe $`git status --porcelain | grep -q .`

    $!`sed -i '' "s/Version = \".*\"/Version = \"{version}\"/" ./core/version.go`

    $!`git add .`
    $!`git diff HEAD --compact-summary`

    if confirm("Commit, tag & push? ({version}) [y/n] > "):
        _, branch  = $!`echo -n $(git branch --show-current)`
        
        if clean_before:
            // implied only our sed version bump made a diff
            $!`git commit -m "Bump version to {version}"`
        else:
            $!`git commit`
        $!`git tag -a "{version}" -m "Bump version to {version}"`
        push_cmd = `git push origin {branch} --tags`
        if no_push:
            print("Not pushing, remember to run", yellow(push_cmd), "later")
        else:
            $!push_cmd

print(green("âœ… Done!"))

// todo rad:
//  count flags RAD-127, could add repeats to also do e.g. tests
//  custom functions (could use go mod edit elsewhere)
